package se.mdh.idt.benji.examples.refactorings

import "http://www.mdh.se/idt/benji/trace/Trace"
import "http://www.mdh.se/idt/benji/examples/refactorings/SimpleCore"

import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_package_name
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.set_package_name
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_package_uri
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.set_package_uri
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_package
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.deleted_package
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.created_package
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.added_package_subpackage
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.created_class
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.added_package_class
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_class_name
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.set_class_name
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_class
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.deleted_class
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_attribute
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_package_class
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_class_attribute
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.deleted_attribute
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.unset_attribute_name
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.created_reference
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.set_reference_type
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.added_class_reference
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.created_attribute
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.added_class_attribute
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_datatype
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_attribute_type
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.set_attribute_type
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_reference
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.deleted_reference
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_reference_type
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_class_super
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_class_reference
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.removed_class_super
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.added_class_super
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.removed_class_attribute
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.removed_class_reference
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.preserved_class_abstract
import se.mdh.idt.benji.examples.refactorings.simplecore.queries.set_class_abstract


// ACP1 - Rename Package - Precondition
pattern RenamePackagePrecondition ($package : Trace) {
	find preserved_package_name($package, _name);
}
// ACP1 - Rename Package - Postcondition
pattern RenamePackagePostcondition ($package : Trace) {
	find set_package_name($package, _name);
}

// ACP2 - Rename Package Uri - Precondition
pattern RenamePackageUriPrecondition ($package : Trace) {
	find preserved_package_uri($package, _uri);
}
// ACP2 - Rename Package Uri - Postcondition
pattern RenamePackageUriPostcondition ($package : Trace) {
	find set_package_uri($package, _uri);
}

// CDPR3 - Delete Package - Precondition
pattern DeletePackagePrecondition ($package : Trace) {
	find preserved_package($package);
}
// CDPR3 - Delete Package - Postcondition
pattern DeletePackagePostcondition ($package : Trace) {
	find deleted_package($package);
}

// AAP4 - Add Package - Precondition
pattern AddPackagePrecondition ($container : Trace) {
	find preserved_package ($container);
}
// AAP4 - Add Package - Postcondition
pattern AddPackagePostcondition ($container : Trace) {
	find preserved_package ($container);
	find created_package ($package);
	find added_package_subpackage($container, $package);
}

// AAC5 - Add Class - Precondition
pattern AddClassPrecondition ($container : Trace) {
	find preserved_package ($container);
}
// AAC5 - Add Class - Postcondition
pattern AddClassPostcondition ($container : Trace) {
	find preserved_package ($container);
	find created_class($class);
	find added_package_class($container, $class);
}

// ACC6 - Rename Class - Precondition
pattern RenameClassPrecondition ($class : Trace) {
	find preserved_class_name($class, _name);
}
// ACC6 - Rename Class - Postcondition
pattern RenameClassPostcondition ($class : Trace) {
	find set_class_name($class, _name);
}

// CDCR7 - Delete Class - Precondition
pattern DeleteClassPrecondition ($class : Trace) {
	find preserved_class($class);
}
// CDCR7 - Delete Class - Postcondition
pattern DeleteClassPostcondition ($class : Trace) {
	find deleted_class($class);
}

// CCAC8 - Extract Class - Precondition
pattern ExtractClassPrecondition($package : Trace, $class : Trace, $attribute : Trace) {
	find preserved_package($package);
	find preserved_class($class);
	find preserved_attribute($attribute);
	find preserved_package_class($package, $class);
	find preserved_class_attribute($class, $attribute);
}
// CCAC8 - Extract Class - Postcondition
pattern ExtractClassPostcondition($package : Trace, $class : Trace, $attribute : Trace) {
	// package
	find preserved_package ($package);
	// class
	find preserved_class ($class);
	find preserved_package_class ($package, $class);
	// attribute
	find deleted_attribute($attribute);
	find unset_attribute_name($attribute, attribute_name);
	// attribute class
	find created_class (attribute_class);
	find added_package_class($package, attribute_class);
	find set_class_name(attribute_class, attribute_class_name);
	attribute_class_name == eval(attribute_name.toFirstUpper);
	// attribute reference
	find created_reference(attribute_reference);
	find set_reference_type(attribute_reference, attribute_class);
	find added_class_reference($class, attribute_reference);
}

// AAA10 - Add Attribute - Precondition
pattern AddAttributePrecondition ($class : Trace) {
	find preserved_class ($class);
}
// AAA10 - Add Attribute - Postcondition
pattern AddAttributePostcondition ($class : Trace) {
	find preserved_class ($class);
	find created_attribute(attribute);
	find added_class_attribute($class, attribute);
}

// ADA11 - Delete Attribute - Precondition
pattern DeleteAttributePrecondition ($attribute : Trace) {
	find preserved_attribute ($attribute);
}
// ADA11 - Delete Attribute - Postcondition
pattern DeleteAttributePostcondition ($attribute : Trace) {
	find deleted_attribute ($attribute);
}

// ACA12 - Change Attribute Type - Precondition
pattern ChangeAttributeTypePrecondition ($attribute : Trace, $datatype : Trace) {
	find preserved_attribute ($attribute);
	find preserved_datatype ($datatype);
	find preserved_attribute_type($attribute, type);
	type != $datatype;
}
// ACA12 - Change Attribute Type - Postcondition
pattern ChangeAttributeTypePostcondition ($attribute : Trace, $datatype : Trace) {
	find preserved_attribute ($attribute);
	find preserved_datatype ($datatype);
	find set_attribute_type($attribute, $datatype);
}

// AACR13 - Add Reference - Precondition
pattern AddReferencePrecondition($class : Trace) {
	find preserved_class ($class);
}
// AACR13 - Add Reference - Postcondition
pattern AddReferencePostcondition($class : Trace) {
	find preserved_class ($class);
	find created_reference(reference);
	find added_class_reference($class, reference);
}

// ADCR14 - Delete Reference - Precondition
pattern DeleteReferencePrecondition ($reference : Trace) {
	find preserved_reference ($reference);
}
// ADCR14 - Delete Reference - Postcondition
pattern DeleteReferencePostcondition ($reference : Trace) {
	find deleted_reference($reference);
}

// ACCR17 - Change Reference Type - Precondition
pattern ChangeReferenceTypePrecondition ($reference : Trace, $class : Trace) {
	find preserved_reference($reference);
	find preserved_class($class);
	find preserved_reference_type($reference, type);
	type != $class;
}
// ACCR17 - Change Reference Type - Postcondition
pattern ChangeReferenceTypePostcondition ($reference : Trace, $class : Trace) {
	find preserved_reference ($reference);
	find preserved_class ($class);
	find set_reference_type($reference, $class);
}

// CADCAR18 - Extract Superclass - Precondition
pattern ExtractSuperclassPrecondition($package : Trace, $class : Trace, $super : Trace, $attribute : Trace, $reference : Trace) {
	find preserved_package ($package);
	find preserved_class ($class);
	find preserved_package_class ($package, $class);
	find preserved_class ($super);
	find preserved_package_class ($package, $super);
	find preserved_class_name ($class, _class_name);
	find preserved_class_super($class, $super);
	find preserved_attribute ($attribute);
	find preserved_class_attribute ($class, $attribute);
	find preserved_reference ($reference);
	find preserved_class_reference ($class, $reference);
}

// CADCAR18 - Extract Superclass - Postcondition
pattern ExtractSuperclassPostcondition($package : Trace, $class : Trace, $super : Trace, $attribute : Trace, $reference : Trace) {
	find preserved_class ($class);
	find preserved_package_class($package, $class);
	find preserved_class_name ($class, class_name);
	find preserved_class ($super);
	find preserved_package_class ($package, $super);
	find removed_class_super($class, $super);
	find created_class(extracted);
	find added_package_class ($package, extracted);
	find set_class_name (extracted, extracted_name);
	extracted_name == eval("Super" + class_name);
	find added_class_super (extracted, $super);
	find preserved_attribute ($attribute);
	find removed_class_attribute($class, $attribute);
	find added_class_attribute(extracted, $attribute);
	find preserved_reference ($reference);
	find removed_class_reference($class, $reference);
	find added_class_reference(extracted, $reference);
	find added_class_super($class, extracted);
}

// ACC19 - Change Class Abstract - Precondition
pattern ChangeClassAbstractPrecondition ($class : Trace) {
	find preserved_class_abstract($class, _abstract);
}
// ACC19 - Change Class Abstract - Postcondition
pattern ChangeClassAbstractPostcondition ($class : Trace) {
	find set_class_abstract($class, _abstract);
}

// CDACA22 - Push Down Attribute - Precondition
pattern PushDownAttributePrecondition ($class : Trace, $super : Trace, $attribute : Trace) {
	find preserved_class ($class);
	find preserved_class ($super);
	find preserved_attribute ($attribute);
	find preserved_class_super ($class, $super);
	find preserved_class_attribute ($super, $attribute);
}
// CDACA22 - Push Down Attribute - Postcondition
pattern PushDownAttributePostcondition ($class : Trace, $super : Trace, $attribute : Trace) {
	find preserved_class ($class);
	find preserved_class ($super);
	find preserved_attribute ($attribute);
	find preserved_class_super ($class, $super);
	find removed_class_attribute($super, $attribute);
	find added_class_attribute($class, $attribute);
}
